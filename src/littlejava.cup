/* Grammar of Littlejava */
import java_cup.runtime.ComplexSymbolFactory;
import java_cup.runtime.ScannerBuffer;
import java_cup.runtime.Symbol;
import java.io.*;
import java.util.*;


parser code {:
    private StringBuilder errors;

    public parser(ComplexSymbolFactory csf, java_cup.runtime.Scanner scan, StringBuilder errors) {
        super(scan,csf);
        this.errors = errors;
    }

    public static Ast.Program parse(Reader reader) throws Exception {
        NamedSymbolFactory csf = new NamedSymbolFactory();
        StringBuilder errors = new StringBuilder();
        ScannerBuffer lexer = new ScannerBuffer(new Lexer(reader, csf));
        parser p = new parser(csf, lexer, errors);
        try {
            return (Ast.Program) p.parse().value;
        } catch (LexerException e) {
            throw e;
        } catch (Exception e) {
            throw new Exception(p.errors.toString(), e);
        }
    }
    
    public static void main(String[] args) throws Exception {
        Ast.Program e = parse(new BufferedReader(new FileReader(args[0])));
        System.out.println(e.prettyPrint(0));
    }

    @Override
    public void report_error(String message, Object info) {
        if (info instanceof ComplexSymbolFactory.ComplexSymbol) {
            ComplexSymbolFactory.ComplexSymbol cs = (ComplexSymbolFactory.ComplexSymbol) info;
            errors.append(message).append(" in the symbol \"").append(cs.getName()).append("\" from line ")
                .append(cs.getLeft().getLine()).append(", column ").append(cs.getLeft().getColumn()).append(" to line ")
                .append(cs.getRight().getLine()).append(", column ").append(cs.getRight().getColumn()).append('\n');
            return;
        }

        errors.append(message);
        if (info instanceof Symbol) {
            if (((Symbol) info).left != 1)
                errors.append(" at the character ").append(((Symbol) info).left).append(" of input\n");
            else
                errors.append('\n');
        } else {
            errors.append('\n');
        }
    }

    @Override
    protected void report_expected_token_ids(){
        List<Integer> ids = expected_token_ids();
        LinkedList<String> list = new LinkedList<String>();
        for (Integer expected : ids){
            list.add(symbl_name_from_id(expected));
        }
        errors.append("instead the expected token classes are ").append(list).append('\n');
  }

:};

// terminals
terminal IF, ELSE, CLASS, WHILE, READLN, PRINTLN, RETURN, THIS, NEW, MAIN, VOID, INT, BOOL, STRING;
terminal String STRING_LITERAL;
terminal Integer INTEGER_LITERAL;
terminal String CNAME;
terminal String IDENT;
terminal TRUE, FALSE;
terminal NULL;
terminal LPAREN, RPAREN, LBRACE, RBRACE, SEMICOLON, DOT;
terminal COMMA;
terminal PLUS, MINUS, TIMES, DIV, LT, GT, LEQ, GEQ, EQ, NEQ, NOT, OROR, ANDAND, ASSIGN;

// non-terminals
non terminal Ast.Program program;
non terminal Ast.Expr exp, bexp, aexp, conj, rexp, bgrd, term, ftr, atom;
non terminal Ast.BinaryOp bop;
non terminal List<Ast.Expr> explist, explist_opt;
non terminal Ast.Stmt stmt;
non terminal List<Ast.Stmt> stmtlist, stmtlist_opt;
non terminal Ast.Typ typ;
non terminal Ast.VarDecl vardecl;
non terminal List<Ast.VarDecl> varDeclList, varDeclList_opt;
non terminal Ast.VarDecl argDecl;
non terminal List<Ast.VarDecl> argDecllist, argDecllist_opt;
non terminal Ast.Meth method;
non terminal List<Ast.Meth> methodlist, methodlist_opt;
non terminal Ast.Clazz classdecl, mainclass;
non terminal List<Ast.Clazz> classlist, classlist_opt;

// the program consists on a mainclass and maybe a list of optional classes
program ::=
    mainclass:c1 classlist_opt:lst
        {: lst.add(0, c1); RESULT = new Ast.Program(lst); :}
    ;

// the main class of a program. It consists on a name, some arguments, some variables, and some methods.
mainclass ::=
    CLASS CNAME:c LBRACE VOID MAIN LPAREN argDecllist_opt:arglst RPAREN LBRACE varDeclList_opt:varlst stmtlist:stmtlst RBRACE RBRACE
    {: Ast.Meth meth = new Ast.Meth(new Ast.VoidTyp(), "main", arglst, varlst, stmtlst);
        ArrayList<Ast.Meth> meths = new ArrayList<>();
        meths.add(meth);
        RESULT = new Ast.Clazz(c, Collections.emptyList(), meths);
    :}
    ;

classdecl ::=
    CLASS CNAME:c LBRACE varDeclList:varlst methodlist_opt:mdlst RBRACE
    {: RESULT = new Ast.Clazz(c, varlst, mdlst); :}
    | CLASS CNAME:c LBRACE methodlist:mdlst RBRACE
    {: RESULT = new Ast.Clazz(c, new ArrayList<>(), mdlst); :}
    | CLASS CNAME:c LBRACE RBRACE
    {: RESULT = new Ast.Clazz(c, new ArrayList<>(), new ArrayList<>()); :}
    ;

classlist_opt ::=
    classlist:lst
        {: RESULT = lst; :}
    | {: RESULT = new ArrayList<Ast.Clazz>(); :}
    ;

classlist ::=
    classdecl:c
        {: ArrayList<Ast.Clazz> arr = new ArrayList<>(); arr.add(c); RESULT = arr; :}
    | classlist:arr classdecl:c
        {: arr.add(c); RESULT = arr; :}
        ;

method ::=
    typ:t IDENT:i LPAREN argDecllist_opt:arglst RPAREN LBRACE varDeclList_opt:varlst stmtlist:stmtlst RBRACE
    {: RESULT = new Ast.Meth(t, i, arglst, varlst, stmtlst); :}
    ;

methodlist_opt ::=
    methodlist:lst
    {: RESULT = lst; :}
    | {: RESULT = new ArrayList<Ast.Meth>(); :}
    ;

methodlist ::=
    method:v
    {: ArrayList<Ast.Meth> arr = new ArrayList<>(); arr.add(v); RESULT = arr; :}
    | methodlist:arr method:v
    {: arr.add(v); RESULT = arr; :}
    ;

vardecl ::=
    typ:t IDENT:i SEMICOLON
    {: RESULT = new Ast.VarDecl(t, i); :}
    ;

varDeclList_opt ::=
    varDeclList:lst
    {: RESULT = lst; :}
    | {: RESULT = new ArrayList<Ast.VarDecl>(); :}
    ;

varDeclList ::=
    vardecl:v
    {: ArrayList<Ast.VarDecl> arr = new ArrayList<>(); arr.add(v); RESULT = arr; :}
    | varDeclList:arr vardecl:v
    {: arr.add(v); RESULT = arr; :}
    ;

argDecl ::=
    typ:t IDENT:i
        {: RESULT = new Ast.VarDecl(t, i); :}
    ;

argDecllist_opt ::=
    argDecllist:lst
    {: RESULT = lst; :}
    | {: RESULT = new ArrayList<Ast.VarDecl>(); :}
    ;

argDecllist ::=
    argDecl:v
    {: ArrayList<Ast.VarDecl> arr = new ArrayList<>(); arr.add(v); RESULT = arr; :}
    | argDecllist:arr COMMA argDecl:v
    {: arr.add(v); RESULT = arr; :}
    ;

typ ::=
    INT
    {: RESULT = new Ast.IntTyp(); :}
    | BOOL
    {: RESULT = new Ast.BoolTyp(); :}
    | STRING
    {: RESULT = new Ast.StringTyp(); :}
    | VOID
    {: RESULT = new Ast.VoidTyp(); :}
    | CNAME:c
    {: RESULT = new Ast.ClazzTyp(c); :}
    ;

stmt ::=
    IF LPAREN exp:e RPAREN LBRACE stmtlist:lst1 RBRACE ELSE LBRACE stmtlist:lst2 RBRACE
        {: RESULT = new Ast.IfStmt(e, lst1, lst2); :}
    | WHILE LPAREN exp:e RPAREN LBRACE stmtlist_opt:lst RBRACE
        {: RESULT = new Ast.WhileStmt(e, lst); :}
    | READLN LPAREN IDENT:i RPAREN SEMICOLON
        {: RESULT = new Ast.ReadlnStmt(i); :}
    | PRINTLN LPAREN exp:e RPAREN SEMICOLON
        {: RESULT = new Ast.PrintlnStmt(e); :}
    | IDENT:i ASSIGN exp:e SEMICOLON
        {: RESULT = new Ast.VarAssignStmt(i, e); :}
    | atom:e1 DOT IDENT:i ASSIGN exp:e2 SEMICOLON
        {: RESULT = new Ast.FieldAssignStmt(e1, i, e2); :}
    | atom:e1 LPAREN explist_opt:lst RPAREN SEMICOLON
        {: RESULT = new Ast.CallStmt(e1, lst); :}
    | RETURN exp:e SEMICOLON
        {: RESULT = new Ast.ReturnStmt(e); :}
    | RETURN SEMICOLON
        {: RESULT = new Ast.ReturnStmt(null); :}
    ;

stmtlist_opt ::=
    stmtlist:lst
        {: RESULT = lst; :}
    | {: RESULT = new ArrayList<Ast.Stmt>(); :}
    ;

stmtlist ::=
    stmt:s
        {: ArrayList<Ast.Stmt> arr = new ArrayList<>(); arr.add(s); RESULT = arr; :}
    | stmtlist:arr stmt:s
        {: arr.add(s); RESULT = arr; :}
    ;

exp ::=
/* Unknown type */
      atom:e
        {: RESULT = e; :}
/* Boolean type */
    /* bexp */
    | bexp:e1 OROR conj:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.LOR, e1, e2); :}
    /* rexp */
    | aexp:e1 bop:op aexp:e2
        {: RESULT = new Ast.BinaryExpr(op, e1, e2); :}
    /* bgrd */
    | NOT bgrd:e
        {: RESULT = new Ast.UnaryExpr(Ast.UnaryOp.LNOT, e); :}
    /* conj */
    | conj:e1 ANDAND rexp:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.LAND, e1, e2); :}
    | TRUE
        {: RESULT = new Ast.BoolLitExpr(true); :}
    | FALSE
        {: RESULT = new Ast.BoolLitExpr(false); :}
/* Integer type */
    /* aexp */
    | aexp:e1 PLUS term:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.PLUS, e1, e2); :}
    | aexp:e1 MINUS term:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.MINUS, e1, e2); :}
    /* term */
    | term:e1 TIMES ftr:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.MUL, e1, e2); :}
    | term:e1 DIV ftr:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.DIV, e1, e2); :}
    /* ftr */
    | INTEGER_LITERAL:i
        {: RESULT = new Ast.IntLitExpr(i); :}
    | MINUS ftr:e
        {: RESULT = new Ast.UnaryExpr(Ast.UnaryOp.NEG, e); :}
    /* String type */
    | STRING_LITERAL:s
        {: RESULT = new Ast.StringLitExpr(s); :}
    ;

bexp ::=
      bexp:e1 OROR conj:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.LOR, e1, e2); :}
    | conj:e
        {: RESULT = e; :}
    ;

conj ::=
      conj:e1 ANDAND rexp:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.LAND, e1, e2); :}
    | rexp:e
        {: RESULT = e; :}
    ;

rexp ::=
      aexp:e1 bop:op aexp:e2
        {: RESULT = new Ast.BinaryExpr(op, e1, e2); :}
    | bgrd:e
        {: RESULT = e; :}
    ;

bop ::=
      LT {: RESULT = Ast.BinaryOp.LT; :}
    | LEQ {: RESULT = Ast.BinaryOp.LE; :}
    | EQ {: RESULT = Ast.BinaryOp.EQ; :}
    | GEQ {: RESULT = Ast.BinaryOp.GE; :}
    | GT {: RESULT = Ast.BinaryOp.GT; :}
    | NEQ {: RESULT = Ast.BinaryOp.NE; :}
    ;

bgrd ::=
      NOT bgrd:e
        {: RESULT = new Ast.UnaryExpr(Ast.UnaryOp.LNOT, e); :}
    | TRUE
        {: RESULT = new Ast.BoolLitExpr(true); :}
    | FALSE
        {: RESULT = new Ast.BoolLitExpr(false); :}
    | atom:e
        {: RESULT = e; :}
    ;

aexp ::=
      aexp:e1 PLUS term:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.PLUS, e1, e2); :}
    | aexp:e1 MINUS term:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.MINUS, e1, e2); :}
    | term:e
        {: RESULT = e; :}
    ;

term ::=
      term:e1 TIMES ftr:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.MUL, e1, e2); :}
    | term:e1 DIV ftr:e2
        {: RESULT = new Ast.BinaryExpr(Ast.BinaryOp.DIV, e1, e2); :}
    | ftr:e
        {: RESULT = e; :}
    ;

ftr ::=
      INTEGER_LITERAL:i
        {: RESULT = new Ast.IntLitExpr(i); :}
    | MINUS ftr:e
        {: RESULT = new Ast.UnaryExpr(Ast.UnaryOp.NEG, e); :}
    | atom:e
        {: RESULT = e; :}
    ;

atom ::=
      atom:e DOT IDENT:ide
        {: RESULT = new Ast.DotExpr(e, ide); :}
    | atom:e LPAREN explist_opt:lst RPAREN
        {: RESULT = new Ast.CallExpr(e, lst); :}
    | THIS
        {: RESULT = new Ast.ThisExpr(); :}
    | IDENT:ide
        {: RESULT = new Ast.IdentExpr(ide); :}
    | NEW CNAME:c LPAREN RPAREN
        {: RESULT = new Ast.NewExpr(c); :}
    | LPAREN exp:e RPAREN
        {: RESULT = e; :}
    | NULL
        {: RESULT = new Ast.NullLitExpr(); :}
    ;

explist_opt ::=
      explist:lst {: RESULT = lst; :}
    | {: RESULT = new ArrayList<Ast.Expr>(); :}
    ;

explist ::=
    exp:e {: ArrayList<Ast.Expr> arr = new ArrayList<>(); arr.add(e); RESULT = arr; :}
    | explist:arr COMMA exp:e {: arr.add(e); RESULT = arr; :}
    ;
